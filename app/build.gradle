apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "ie.macinnes.tvheadend"
        minSdkVersion 22
        targetSdkVersion 24
        versionCode 48
        versionName '0.2.' + versionCode
        setProperty("archivesBaseName", "${applicationId}_${versionName}")

        buildConfigField "boolean", "ACRA_ENABLED", "false"
        buildConfigField "String", "ACRA_REPORT_URI", "\"\""
    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (rootProject.file("acra.properties").exists()) {
                buildConfigField "boolean", "ACRA_ENABLED", "true"
                def acraProperties = new Properties()
                acraProperties.load(new FileInputStream(rootProject.file("acra.properties")))
                buildConfigField "String", "ACRA_REPORT_URI", "\"" + acraProperties.report_uri + "\""
            }
        }
    }
    productFlavors {
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //    compile(name:'vlc-android-sdk-release', ext:'aar') // Used for testing local libvlc builds
    compile project(':htsp')
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:leanback-v17:24.2.1'
    compile 'com.android.support:preference-leanback-v17:24.2.1'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.9'
    compile 'de.mrmaffen:vlc-android-sdk:1.9.8'
    compile 'ch.acra:acra:4.9.0'
}
