import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'

ext {
    git = Grgit.open()
    gitVersionName = "${git.describe()}"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "ie.macinnes.tvheadend"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName gitVersionName
        setProperty("archivesBaseName", "${applicationId}_${versionName}")

        buildConfigField "boolean", "ACRA_ENABLED", "false"
        buildConfigField "String", "ACRA_REPORT_URI", "\"\""
    }

    signingConfigs {
        if (rootProject.file("keystore.properties").exists()) {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

            release {
                storeFile new File(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (signingConfigs.hasProperty("release")) {
                signingConfig signingConfigs.release
            }

            if (rootProject.file("acra.properties").exists()) {
                buildConfigField "boolean", "ACRA_ENABLED", "true"
                def acraProperties = new Properties()
                acraProperties.load(new FileInputStream(rootProject.file("acra.properties")))
                buildConfigField "String", "ACRA_REPORT_URI", "\"" + acraProperties.report_uri + "\""
            }
        }
    }

    productFlavors {
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // compile(name:'vlc-android-sdk-release', ext:'aar') // Used for testing local libvlc builds
    compile project(':htsp')
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:leanback-v17:25.0.0'
    compile 'com.android.support:preference-leanback-v17:25.0.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.9'
    compile 'de.mrmaffen:vlc-android-sdk:1.9.8'
    compile 'ch.acra:acra:4.9.0'
}
